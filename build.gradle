plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'

}

group 'com.abdelhalim'
archivesBaseName = "OSSRH-library-publish"
version '1.0-SNAPSHOT'


repositories {
    mavenCentral()
}
signing {
    sign publishing.publications.mavenJava
}
dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "$ossrhUsername"
                password = "$ossrhPassword"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.abdelhalim'
            artifactId 'com.abdelhalim'
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'OSSRH-library-publish'
                packaging = 'jar'
                description = 'This is an example of library.'
                url = 'https://example.org'

                scm {
                    connection = 'scm:git:https://github.com/dmart28/reveno.git'
                    developerConnection = 'scm:git:git@github.com:dmart28/reveno.git'
                    url = 'https://github.com/dmart28/reveno'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'johndoe'
                        name = 'John Doe'
                        email = 'john@doe.im'
                    }
                }
            }
        }
    }
}